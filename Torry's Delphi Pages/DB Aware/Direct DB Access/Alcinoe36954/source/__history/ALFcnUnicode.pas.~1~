{*************************************************************
www:          http://sourceforge.net/projects/alcinoe/              
Author(s):    Stéphane Vander Clock (svanderclock@arkadia.com)
Sponsor(s):   Arkadia SA (http://www.arkadia.com)
							
product:      Alcinoe Unicode Functions
Version:      3.50

Description:  This unit contains a Unicode support library along
              with some additional files to use WideStrings/Unicode
              strings within your application.

Legal issues: Copyright (C) 1999-2008 by Arkadia Software Engineering

              This software is provided 'as-is', without any express
              or implied warranty.  In no event will the author be
              held liable for any  damages arising from the use of
              this software.

              Permission is granted to anyone to use this software
              for any purpose, including commercial applications,
              and to alter it and redistribute it freely, subject
              to the following restrictions:

              1. The origin of this software must not be
                 misrepresented, you must not claim that you wrote
                 the original software. If you use this software in
                 a product, an acknowledgment in the product
                 documentation would be appreciated but is not
                 required.

              2. Altered source versions must be plainly marked as
                 such, and must not be misrepresented as being the
                 original software.

              3. This notice may not be removed or altered from any
                 source distribution.

              4. You must register this software by sending a picture
                 postcard to the author. Use a nice stamp and mention
                 your name, street address, EMail address and any
                 comment you like to say.

Know bug :

History :     14/11/2005: Add Function ALStringToWideString,
                          AlWideStringToString, ALUTF8Encode,
                          ALUTF8decode, ALGetCodePageFromName;
              29/10/2007: Add function ALGetCodePageFromLCID
              10/11/2007: update ALWideRemoveDiacritic to handle
                          char like U+1E69 (html code &#7785;)

Link :

Please send all your feedback to svanderclock@arkadia.com
**************************************************************}
unit ALFcnUnicode;

interface

uses windows;

Function ALWideNormalize(S: Widestring): Widestring;
Function ALWideRemoveDiacritic(S: Widestring): Widestring;
Function ALWideExpandLigatures(S: Widestring): Widestring;
Function ALWideUpperCaseNoDiacritic(S: Widestring): Widestring;
Function ALWideLowerCaseNoDiacritic(S: Widestring): Widestring;
Function ALUTF8RemoveDiacritic(S: UTF8String): UTF8String;
Function ALUTF8ExpandLigatures(S: UTF8String): UTF8String;
Function ALUTF8UpperCaseNoDiacritic(S: UTF8String): UTF8String;
Function ALUTF8LowerCaseNoDiacritic(S: UTF8String): UTF8String;
Function ALUTF8Normalize(S: UTF8String): UTF8String;
function ALUTF8UpperCase(const s: UTF8String): UTF8String;
function ALUTF8LowerCase(const s: UTF8String): UTF8String;
function AlUTF8DetectBOM(const P: PChar; const Size: Integer): Boolean;
function ALUTF8CharSize(const P: PChar; const Size: Integer): Integer;
function ALUTF8CharCount(const P: PChar; const Size: Integer): Integer; overload;
function ALUTF8CharCount(const S: Utf8String): Integer; overload;
Function ALUTF8Trunc(s:UTF8string; Count: Integer): UTF8String;
Function ALUTF8UpperFirstChar(s:UTF8string): UTF8String;
Function ALUTF8LowerCaseFirstCharUpper(s:UTF8string): UTF8String;
Function ALStringToWideString(const S: string; aCodePage: Word): WideString;
function AlWideStringToString(const WS: WideString; aCodePage: Word): string;
Function ALUTF8Encode(const S: string; aCodePage: Word): UTF8String;
Function ALUTF8decode(const S: UTF8String; aCodePage: Word): String;
Function ALGetCodePageFromName(Acharset:String): Word;
Function ALGetCodePageFromLCID(aLCID:Integer): Word;

implementation

uses SysUtils,
     AlFcnString;

{*******************}
{Normalize an string
 ie: l''étè sur l''europe => l-ete-sur-l-europe}
Function ALWideNormalize(S: Widestring): Widestring;
Var i,j: integer;
    TmpWideStr: WideString;
Begin
  TmpWideStr := ALWideExpandLigatures(
                                     ALWideRemoveDiacritic(
                                                           Widelowercase(s)
                                                          )
                                     );
  SetLength(Result,length(TmpWideStr));
  j := 0;
  For i := 1 to length(TmpWideStr) do begin
    if IsCharAlphaNumericW(TmpWideStr[i]) then begin
      inc(j);
      result[j] := TmpWideStr[i];
    end
    else if (
             (j >= 1) and
             (result[j] <> '-')
            )
    then begin
      inc(j);
      result[j] := '-';
    end;

  end;
  While (J > 0) and (result[j] = '-') do dec(j);
  setlength(result,j);
end;

{*************************}
{remove accented character}
Function ALWideRemoveDiacritic(S: Widestring): Widestring;
var LenS, LenTmpWideStr: Integer;
    i,J: integer;
    TmpWideStr: WideString;

    {----------------------------------------------------------------}
    Function internalGetCompositeCharSize(aChar: WideString): integer;
    Begin
      Result := FoldStringW(MAP_COMPOSITE, PwideChar(aChar), length(aChar), nil, 0);
    end;

begin
  result := '';
  If s = '' then exit;

  {upper the result}
  LenS := length(S);

  {remove diacritic}
  LenTmpWideStr := FoldStringW(MAP_COMPOSITE, PwideChar(S), LenS, nil, 0);
  setlength(TmpWideStr,LenTmpWideStr);
  FoldStringW(MAP_COMPOSITE, PwideChar(S), LenS, PwideChar(TmpWideStr), LenTmpWideStr);
  i := 1;
  J := 1;
  SetLength(result,lenS);
  while J <= lenS do begin
    Result[j] := TmpWideStr[i];
    if S[j] <> TmpWideStr[i] then inc(i,internalGetCompositeCharSize(S[j])) //some Diacritic can have a length of 3 in composite (ie: U+1E69)
    else inc(i);
    inc(j);
  end;
end;

{********************************************************
 Expand all ligature characters so that they are represented by their two-character equivalent.
 For example, the ligature 'æ' expands to the two characters 'a' and 'e'.}
Function ALWideExpandLigatures(S: Widestring): Widestring;
Const aMAP_EXPAND_LIGATURES = $2000;
var LenS, LenResult: Integer;
begin
  result := '';
  If s = '' then exit;

  {upper the result}
  LenS := length(S);

  {EXPAND LIGATURES}
  LenResult := FoldStringW(aMAP_EXPAND_LIGATURES, PwideChar(S), LenS, nil, 0);
  setlength(Result,LenResult);
  FoldStringW(aMAP_EXPAND_LIGATURES, PwideChar(S), LenS, PwideChar(Result), LenResult);
end;

{*************************************************************}
Function ALWideUpperCaseNoDiacritic(S: Widestring): Widestring;
begin
  Result := ALWideRemoveDiacritic(WideUppercase(s));
end;

{*************************************************************}
Function ALWideLowerCaseNoDiacritic(S: Widestring): Widestring;
begin
  Result := ALWideRemoveDiacritic(Widelowercase(s));
end;

{********************************************************}
Function ALUTF8RemoveDiacritic(S: UTF8String): UTF8String;
begin
  Result := utf8Encode(
                       ALWideRemoveDiacritic(
                                             UTF8Decode(S)
                                            )
                      );
end;

{********************************************************}
Function ALUTF8ExpandLigatures(S: UTF8String): UTF8String;
begin
  Result := utf8Encode(
                       ALWideExpandLigatures(
                                             UTF8Decode(S)
                                            )
                      );
end;

{*************************************************************}
Function ALUTF8UpperCaseNoDiacritic(S: UTF8String): UTF8String;
begin
  Result := utf8Encode(
                       ALWideUpperCaseNoDiacritic(
                                                  UTF8Decode(S)
                                                 )
                      );
end;

{*************************************************************}
Function ALUTF8LowerCaseNoDiacritic(S: UTF8String): UTF8String;
begin
  Result := utf8Encode(
                       ALWideLowerCaseNoDiacritic(
                                                  UTF8Decode(S)
                                                 )
                      );
end;

{**************************************************}
Function ALUTF8Normalize(S: UTF8String): UTF8String;
begin
  Result := utf8Encode(
                       ALWideNormalize(
                                       UTF8Decode(S)
                                      )
                      );
end;

{********************************************************}
function ALUTF8UpperCase(const s: UTF8String): UTF8String;
begin
  result := utf8encode(WideUppercase(utf8Decode(s)));
end;

{********************************************************}
function ALUTF8LowerCase(const s: UTF8String): UTF8String;
begin
  result := utf8encode(WideLowerCase(utf8Decode(s)));
end;

{*********************************************************************}
function AlUTF8DetectBOM(const P: PChar; const Size: Integer): Boolean;
var Q: PChar;
begin
  Result := False;
  if Assigned(P) and (Size >= 3) and (P^ = #$EF) then begin
    Q := P;
    Inc(Q);
    if Q^ = #$BB then begin
      Inc(Q);
      if Q^ = #$BF then Result := True;
    end;
  end;
end;

{****************************************************}
{give on how many bits is encoded the first char in P}
function ALUTF8CharSize(const P: PChar; const Size: Integer): Integer;
var C: Byte;
    I: Integer;
    Q: PChar;
begin
  if not Assigned(P) or (Size <= 0) then begin
    Result := 0;
    exit;
  end;
  C := Ord(P^);
  if C < $80 then Result := 1 // 1-byte (US-ASCII value)
  else if C and $C0 = $80 then Result := 1 // invalid encoding
  else begin
    // multi-byte character
    if C and $20 = 0 then Result := 2
    else if C and $10 = 0 then Result := 3
    else if C and $08 = 0 then Result := 4
    else begin
      Result := 1; // invalid encoding
      exit;
    end;
    if Size < Result then exit; // incomplete encoding
    Q := P;
    Inc(Q);
    For I := 1 to Result - 1 do
      if Ord(Q^) and $C0 <> $80 then begin
          Result := 1; // invalid encoding
          exit;
      end
      else Inc(Q);
  end;
end;

{******************************}
{give on how many char are in P}
function ALUTF8CharCount(const P: PChar; const Size: Integer): Integer;
var Q    : PChar;
    L, C : Integer;
begin
  Q := P;
  L := Size;
  Result := 0;
  While L > 0 do begin
    C := ALUTF8CharSize(Q, L);
    Dec(L, C);
    Inc(Q, C);
    Inc(Result);
  end;
end;

{******************************}
{give on how many char are in P}
function ALUTF8CharCount(const S: UTF8String): Integer;
begin
  Result := ALUTF8CharCount(Pointer(S), Length(S));
end;

{******************************}
{give on how many char are in P}
Function ALUTF8Trunc(s:UTF8string; Count: Integer): UTF8String;
var Q    : PChar;
    L, C, M : Integer;
begin
  L := Length(S);
  If (L = 0) or (Count >= L) then Begin
    Result := S;
    Exit;
  end;

  Q := Pchar(S);
  M := 0;

  While L > 0 do begin
    C := ALUTF8CharSize(Q, L);
    If M + C > Count then break;
    inc(M, C);
    Dec(L, C);
    Inc(Q, C);
  end;

  Result := ALCopyStr(S,1,M);
end;

{*****************************}
{Uppercase only the First char}
Function ALUTF8UpperFirstChar(s:UTF8string): UTF8String;
var tmpWideStr: WideString;
begin
  TmpWideStr := UTF8Decode(S);
  result := utf8encode(WideUpperCase(copy(TmpWideStr,1,1)) + copy(TmpWideStr,2,MaxInt));
end;

{***************************************************************}
Function ALUTF8LowerCaseFirstCharUpper(s:UTF8string): UTF8String;
var tmpWideStr: WideString;
begin
  TmpWideStr := UTF8Decode(S);
  result := utf8encode(WideUpperCase(copy(TmpWideStr,1,1)) + WidelowerCase(copy(TmpWideStr,2,MaxInt)));
end;

{****************************************************}
Function ALGetCodePageFromName(Acharset:String): Word;
begin
  Acharset := Trim(AlLowerCase(ACharset));

  if acharset='utf-8' then result := 65001 // unicode (utf-8)
  else if acharset='iso-8859-1' then result := 28591 // western european (iso)
  else if acharset='iso-8859-2' then result := 28592 // central european (iso)
  else if acharset='iso-8859-3' then result := 28593 // latin 3 (iso)
  else if acharset='iso-8859-4' then result := 28594 // baltic (iso)
  else if acharset='iso-8859-5' then result := 28595 // cyrillic (iso)
  else if acharset='iso-8859-6' then result := 28596 // arabic (iso)
  else if acharset='iso-8859-7' then result := 28597 // greek (iso)
  else if acharset='iso-8859-8' then result := 28598 // hebrew (iso-visual)
  else if acharset='iso-8859-9' then result := 28599 // turkish (iso)
  else if acharset='iso-8859-13' then result := 28603 // estonian (iso)
  else if acharset='iso-8859-15' then result := 28605 // latin 9 (iso)
  else if acharset='ibm037' then result := 37 // ibm ebcdic (us-canada)
  else if acharset='ibm437' then result := 437 // oem united states
  else if acharset='ibm500' then result := 500 // ibm ebcdic (international)
  else if acharset='asmo-708' then result := 708 // arabic (asmo 708)
  else if acharset='dos-720' then result := 720 // arabic (dos)
  else if acharset='ibm737' then result := 737 // greek (dos)
  else if acharset='ibm775' then result := 775 // baltic (dos)
  else if acharset='ibm850' then result := 850 // western european (dos)
  else if acharset='ibm852' then result := 852 // central european (dos)
  else if acharset='ibm855' then result := 855 // oem cyrillic
  else if acharset='ibm857' then result := 857 // turkish (dos)
  else if acharset='ibm00858' then result := 858 // oem multilingual latin i
  else if acharset='ibm860' then result := 860 // portuguese (dos)
  else if acharset='ibm861' then result := 861 // icelandic (dos)
  else if acharset='dos-862' then result := 862 // hebrew (dos)
  else if acharset='ibm863' then result := 863 // french canadian (dos)
  else if acharset='ibm864' then result := 864 // arabic (864)
  else if acharset='ibm865' then result := 865 // nordic (dos)
  else if acharset='cp866' then result := 866 // cyrillic (dos)
  else if acharset='ibm869' then result := 869 // greek, modern (dos)
  else if acharset='ibm870' then result := 870 // ibm ebcdic (multilingual latin-2)
  else if acharset='windows-874' then result := 874 // thai (windows)
  else if acharset='cp875' then result := 875 // ibm ebcdic (greek modern)
  else if acharset='shift_jis' then result := 932 // japanese (shift-jis)
  else if acharset='gb2312' then result := 936 // chinese simplified (gb2312)
  else if acharset='ks_c_5601-1987' then result := 949 // korean
  else if acharset='big5' then result := 950 // chinese traditional (big5)
  else if acharset='ibm1026' then result := 1026 // ibm ebcdic (turkish latin-5)
  else if acharset='ibm01047' then result := 1047 // ibm latin-1
  else if acharset='ibm01140' then result := 1140 // ibm ebcdic (us-canada-euro)
  else if acharset='ibm01141' then result := 1141 // ibm ebcdic (germany-euro)
  else if acharset='ibm01142' then result := 1142 // ibm ebcdic (denmark-norway-euro)
  else if acharset='ibm01143' then result := 1143 // ibm ebcdic (finland-sweden-euro)
  else if acharset='ibm01144' then result := 1144 // ibm ebcdic (italy-euro)
  else if acharset='ibm01145' then result := 1145 // ibm ebcdic (spain-euro)
  else if acharset='ibm01146' then result := 1146 // ibm ebcdic (uk-euro)
  else if acharset='ibm01147' then result := 1147 // ibm ebcdic (france-euro)
  else if acharset='ibm01148' then result := 1148 // ibm ebcdic (international-euro)
  else if acharset='ibm01149' then result := 1149 // ibm ebcdic (icelandic-euro)
  else if acharset='utf-16' then result := 1200 // unicode
  else if acharset='unicodefffe' then result := 1201 // unicode (big-endian)
  else if acharset='windows-1250' then result := 1250 // central european (windows)
  else if acharset='windows-1251' then result := 1251 // cyrillic (windows)
  else if acharset='windows-1252' then result := 1252 // western european (windows)
  else if acharset='windows-1253' then result := 1253 // greek (windows)
  else if acharset='windows-1254' then result := 1254 // turkish (windows)
  else if acharset='windows-1255' then result := 1255 // hebrew (windows)
  else if acharset='windows-1256' then result := 1256 // arabic (windows)
  else if acharset='windows-1257' then result := 1257 // baltic (windows)
  else if acharset='windows-1258' then result := 1258 // vietnamese (windows)
  else if acharset='johab' then result := 1361 // korean (johab)
  else if acharset='macintosh' then result := 10000 // western european (mac)
  else if acharset='x-mac-japanese' then result := 10001 // japanese (mac)
  else if acharset='x-mac-chinesetrad' then result := 10002 // chinese traditional (mac)
  else if acharset='x-mac-korean' then result := 10003 // korean (mac)
  else if acharset='x-mac-arabic' then result := 10004 // arabic (mac)
  else if acharset='x-mac-hebrew' then result := 10005 // hebrew (mac)
  else if acharset='x-mac-greek' then result := 10006 // greek (mac)
  else if acharset='x-mac-cyrillic' then result := 10007 // cyrillic (mac)
  else if acharset='x-mac-chinesesimp' then result := 10008 // chinese simplified (mac)
  else if acharset='x-mac-romanian' then result := 10010 // romanian (mac)
  else if acharset='x-mac-ukrainian' then result := 10017 // ukrainian (mac)
  else if acharset='x-mac-thai' then result := 10021 // thai (mac)
  else if acharset='x-mac-ce' then result := 10029 // central european (mac)
  else if acharset='x-mac-icelandic' then result := 10079 // icelandic (mac)
  else if acharset='x-mac-turkish' then result := 10081 // turkish (mac)
  else if acharset='x-mac-croatian' then result := 10082 // croatian (mac)
  else if acharset='x-chinese-cns' then result := 20000 // chinese traditional (cns)
  else if acharset='x-cp20001' then result := 20001 // tca taiwan
  else if acharset='x-chinese-eten' then result := 20002 // chinese traditional (eten)
  else if acharset='x-cp20003' then result := 20003 // ibm5550 taiwan
  else if acharset='x-cp20004' then result := 20004 // teletext taiwan
  else if acharset='x-cp20005' then result := 20005 // wang taiwan
  else if acharset='x-ia5' then result := 20105 // western european (ia5)
  else if acharset='x-ia5-german' then result := 20106 // german (ia5)
  else if acharset='x-ia5-swedish' then result := 20107 // swedish (ia5)
  else if acharset='x-ia5-norwegian' then result := 20108 // norwegian (ia5)
  else if acharset='us-ascii' then result := 20127 // us-ascii
  else if acharset='x-cp20261' then result := 20261 // t.61
  else if acharset='x-cp20269' then result := 20269 // iso-6937
  else if acharset='ibm273' then result := 20273 // ibm ebcdic (germany)
  else if acharset='ibm277' then result := 20277 // ibm ebcdic (denmark-norway)
  else if acharset='ibm278' then result := 20278 // ibm ebcdic (finland-sweden)
  else if acharset='ibm280' then result := 20280 // ibm ebcdic (italy)
  else if acharset='ibm284' then result := 20284 // ibm ebcdic (spain)
  else if acharset='ibm285' then result := 20285 // ibm ebcdic (uk)
  else if acharset='ibm290' then result := 20290 // ibm ebcdic (japanese katakana)
  else if acharset='ibm297' then result := 20297 // ibm ebcdic (france)
  else if acharset='ibm420' then result := 20420 // ibm ebcdic (arabic)
  else if acharset='ibm423' then result := 20423 // ibm ebcdic (greek)
  else if acharset='ibm424' then result := 20424 // ibm ebcdic (hebrew)
  else if acharset='x-ebcdic-koreanextended' then result := 20833 // ibm ebcdic (korean extended)
  else if acharset='ibm-thai' then result := 20838 // ibm ebcdic (thai)
  else if acharset='koi8-r' then result := 20866 // cyrillic (koi8-r)
  else if acharset='ibm871' then result := 20871 // ibm ebcdic (icelandic)
  else if acharset='ibm880' then result := 20880 // ibm ebcdic (cyrillic russian)
  else if acharset='ibm905' then result := 20905 // ibm ebcdic (turkish)
  else if acharset='ibm00924' then result := 20924 // ibm latin-1
  else if acharset='euc-jp' then result := 20932 // japanese (jis 0208-1990 and 0212-1990)
  else if acharset='x-cp20936' then result := 20936 // chinese simplified (gb2312-80)
  else if acharset='x-cp20949' then result := 20949 // korean wansung
  else if acharset='cp1025' then result := 21025 // ibm ebcdic (cyrillic serbian-bulgarian)
  else if acharset='koi8-u' then result := 21866 // cyrillic (koi8-u)
  else if acharset='x-europa' then result := 29001 // europa
  else if acharset='iso-8859-8-i' then result := 38598 // hebrew (iso-logical)
  else if acharset='iso-2022-jp' then result := 50220 // japanese (jis)
  else if acharset='csiso2022jp' then result := 50221 // japanese (jis-allow 1 byte kana)
  else if acharset='iso-2022-jp' then result := 50222 // japanese (jis-allow 1 byte kana - so/si)
  else if acharset='iso-2022-kr' then result := 50225 // korean (iso)
  else if acharset='x-cp50227' then result := 50227 // chinese simplified (iso-2022)
  else if acharset='euc-jp' then result := 51932 // japanese (euc)
  else if acharset='euc-cn' then result := 51936 // chinese simplified (euc)
  else if acharset='euc-kr' then result := 51949 // korean (euc)
  else if acharset='hz-gb-2312' then result := 52936 // chinese simplified (hz)
  else if acharset='gb18030' then result := 54936 // chinese simplified (gb18030)
  else if acharset='x-iscii-de' then result := 57002 // iscii devanagari
  else if acharset='x-iscii-be' then result := 57003 // iscii bengali
  else if acharset='x-iscii-ta' then result := 57004 // iscii tamil
  else if acharset='x-iscii-te' then result := 57005 // iscii telugu
  else if acharset='x-iscii-as' then result := 57006 // iscii assamese
  else if acharset='x-iscii-or' then result := 57007 // iscii oriya
  else if acharset='x-iscii-ka' then result := 57008 // iscii kannada
  else if acharset='x-iscii-ma' then result := 57009 // iscii malayalam
  else if acharset='x-iscii-gu' then result := 57010 // iscii gujarati
  else if acharset='x-iscii-pa' then result := 57011 // iscii punjabi
  else if acharset='utf-7' then result := 65000 // unicode (utf-7)
  else if acharset='utf-32' then result := 65005 // unicode (utf-32)
  else if acharset='utf-32be' then result := 65006 // unicode (utf-32 big-endian)
  else Result := 0; //Default ansi code page
end;

{**************************************************}
Function ALGetCodePageFromLCID(aLCID:Integer): Word;
Begin
  Case aLCID of
    1025: Result := 1256; //Arabic (Saudi Arabia)
    1026: Result := 1251; //Bulgarian
    1027: Result := 1252; //Catalan
    1028: Result := 950; //Chinese (Taiwan)
    1029: Result := 1250; //Czech
    1030: Result := 1252; //Danish
    1031: Result := 1252; //German (Germany)
    1032: Result := 1253; //Greek
    1033: Result := 1252; //English (United States)
    1034: Result := 1252; //Spanish (Traditional Sort)
    1035: Result := 1252; //Finnish
    1036: Result := 1252; //French (France)
    1037: Result := 1255; //Hebrew
    1038: Result := 1250; //Hungarian
    1039: Result := 1252; //Icelandic
    1040: Result := 1252; //Italian (Italy)
    1041: Result := 932; //Japanese
    1042: Result := 949; //Korean
    1043: Result := 1252; //Dutch (Netherlands)
    1044: Result := 1252; //Norwegian (Bokmal)
    1045: Result := 1250; //Polish
    1046: Result := 1252; //Portuguese (Brazil)
    1048: Result := 1250; //Romanian
    1049: Result := 1251; //Russian
    1050: Result := 1250; //Croatian
    1051: Result := 1250; //Slovak
    1052: Result := 1250; //Albanian
    1053: Result := 1252; //Swedish
    1054: Result := 874; //Thai
    1055: Result := 1254; //Turkish
    1056: Result := 1256; //Urdu
    1057: Result := 1252; //Indonesian
    1058: Result := 1251; //Ukrainian
    1059: Result := 1251; //Belarusian
    1060: Result := 1250; //Slovenian
    1061: Result := 1257; //Estonian
    1062: Result := 1257; //Latvian
    1063: Result := 1257; //Lithuanian
    1065: Result := 1256; //Farsi
    1066: Result := 1258; //Vietnamese
    1068: Result := 1254; //Azeri (Latin)
    1069: Result := 1252; //Basque
    1071: Result := 1251; //FYRO Macedonian
    1078: Result := 1252; //Afrikaans
    1080: Result := 1252; //Faroese
    1086: Result := 1252; //Malay (Malaysia)
    1087: Result := 1251; //Kazakh
    1088: Result := 1251; //Kyrgyz (Cyrillic)
    1089: Result := 1252; //Swahili
    1091: Result := 1254; //Uzbek (Latin)
    1092: Result := 1251; //Tatar
    1104: Result := 1251; //Mongolian (Cyrillic)
    1110: Result := 1252; //Galician
    2049: Result := 1256; //Arabic (Iraq)
    2052: Result := 936; //Chinese (PRC)
    2055: Result := 1252; //German (Switzerland)
    2057: Result := 1252; //English (United Kingdom)
    2058: Result := 1252; //Spanish (Mexico)
    2060: Result := 1252; //French (Belgium)
    2064: Result := 1252; //Italian (Switzerland)
    2067: Result := 1252; //Dutch (Belgium)
    2068: Result := 1252; //Norwegian (Nynorsk)
    2070: Result := 1252; //Portuguese (Portugal)
    2074: Result := 1250; //Serbian (Latin)
    2077: Result := 1252; //Swedish (Finland)
    2092: Result := 1251; //Azeri (Cyrillic)
    2110: Result := 1252; //Malay (Brunei Darussalam)
    2115: Result := 1251; //Uzbek (Cyrillic)
    3073: Result := 1256; //Arabic (Egypt)
    3076: Result := 950; //Chinese (Hong Kong S.A.R.)
    3079: Result := 1252; //German (Austria)
    3081: Result := 1252; //English (Australia)
    3082: Result := 1252; //Spanish (International Sort)
    3084: Result := 1252; //French (Canada)
    3098: Result := 1251; //Serbian (Cyrillic)
    4097: Result := 1256; //Arabic (Libya)
    4100: Result := 936; //Chinese (Singapore)
    4103: Result := 1252; //German (Luxembourg)
    4105: Result := 1252; //English (Canada)
    4106: Result := 1252; //Spanish (Guatemala)
    4108: Result := 1252; //French (Switzerland)
    5121: Result := 1256; //Arabic (Algeria)
    5124: Result := 950; //Chinese (Macau S.A.R.)
    5127: Result := 1252; //German (Liechtenstein)
    5129: Result := 1252; //English (New Zealand)
    5130: Result := 1252; //Spanish (Costa Rica)
    5132: Result := 1252; //French (Luxembourg)
    6145: Result := 1256; //Arabic (Morocco)
    6153: Result := 1252; //English (Ireland)
    6154: Result := 1252; //Spanish (Panama)
    6156: Result := 1252; //French (Monaco)
    7169: Result := 1256; //Arabic (Tunisia)
    7177: Result := 1252; //English (South Africa)
    7178: Result := 1252; //Spanish (Dominican Republic)
    8193: Result := 1256; //Arabic (Oman)
    8201: Result := 1252; //English (Jamaica)
    8202: Result := 1252; //Spanish (Venezuela)
    9217: Result := 1256; //Arabic (Yemen)
    9225: Result := 1252; //English (Caribbean)
    9226: Result := 1252; //Spanish (Colombia)
    10241: Result := 1256; //Arabic (Syria)
    10249: Result := 1252; //English (Belize)
    10250: Result := 1252; //Spanish (Peru)
    11265: Result := 1256; //Arabic (Jordan)
    11273: Result := 1252; //English (Trinidad)
    11274: Result := 1252; //Spanish (Argentina)
    12289: Result := 1256; //Arabic (Lebanon)
    12297: Result := 1252; //English (Zimbabwe)
    12298: Result := 1252; //Spanish (Ecuador)
    13313: Result := 1256; //Arabic (Kuwait)
    13321: Result := 1252; //English (Philippines)
    13322: Result := 1252; //Spanish (Chile)
    14337: Result := 1256; //Arabic (U.A.E.)
    14346: Result := 1252; //Spanish (Uruguay)
    15361: Result := 1256; //Arabic (Bahrain)
    15370: Result := 1252; //Spanish (Paraguay)
    16385: Result := 1256; //Arabic (Qatar)
    16394: Result := 1252; //Spanish (Bolivia)
    17418: Result := 1252; //Spanish (El Salvador)
    18442: Result := 1252; //Spanish (Honduras)
    19466: Result := 1252; //Spanish (Nicaragua)
    20490: Result := 1252; //Spanish (Puerto Rico)
    else Result := 0; //Default ansi code page
  end;
end;

{**************************************************************************}
Function ALStringToWideString(const S: string; aCodePage: Word): WideString;
var InputLength,
    OutputLength: Integer;
begin
  InputLength := Length(S);
  OutputLength := MultiByteToWideChar(aCodePage, 0, PChar(S), InputLength, nil, 0);
  SetLength(Result, OutputLength);
  MultiByteToWideChar(aCodePage, 0, PChar(S), InputLength, PWideChar(Result), OutputLength);
end;

{***************************************************************************}
function AlWideStringToString(const WS: WideString; aCodePage: Word): string;
var InputLength,
    OutputLength: Integer;
begin
  InputLength := Length(WS);
  OutputLength := WideCharToMultiByte(aCodePage, 0, PWideChar(WS), InputLength, nil, 0, nil, nil);
  SetLength(Result, OutputLength);
  WideCharToMultiByte(aCodePage, 0, PWideChar(WS), InputLength, PChar(Result), OutputLength, nil, nil);
end;

{******************************************************************}
Function ALUTF8Encode(const S: string; aCodePage: Word): UTF8String;
begin
  Result := UTF8Encode(ALStringToWideString(S, aCodePage));
end;

{******************************************************************}
Function ALUTF8decode(const S: UTF8String; aCodePage: Word): String;
begin
  Result := ALWideStringToString(UTF8Decode(S), aCodePage);
end;

end.
